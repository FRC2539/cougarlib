plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'edu.wpi.first.GradleRIO'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

wpi.maven.useLocal = false

allprojects {
    group 'com.team2539'
    version '2022.0.1'

    repositories {
        mavenCentral()
    }
}

dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    implementation project('cougar-swerve-lib')
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'cougarlib'
                description = 'A library containing the common code for all of our java robots.'
                url = 'https://github.com/FRC2539/cougarlib'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/FRC2539/cougarlib/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'we-gold'
                        name = 'Weaver Goldman'
                        email = 'we.goldm@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/FRC2539/cougarlib.git'
                    developerConnection = 'scm:git:ssh://github.com/FRC2539/cougarlib.git'
                    url = 'https://github.com/FRC2539/cougarlib'
                }
            }
        }
    }
    
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username System.getenv("MAVEN_USERNAME")
                password System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
